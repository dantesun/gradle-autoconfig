import org.gradle.initialization.DefaultGradlePropertiesLoader
import static groovy.io.FileType.DIRECTORIES
import static groovy.io.FileType.FILES
import static groovy.io.FileVisitResult.SKIP_SUBTREE

class ConfigUtils {
    private static final Logger logger = Logging.getLogger(ConfigUtils)

    public static final AUTO_CONFIG_SWITCH = "org.dsun.gradle.autoconfig"
    public static final String BUILD_SRC = "buildSrc"
    public static final String ROOT_PATH = ":"

    static boolean isAutoConfigOn(Project project) {
        return "true" == project.properties.get(AUTO_CONFIG_SWITCH)
    }

    static boolean isAutoConfigurable(Project project) {
        if (isBuildSrc(project)) {
            project.logger.info("[GradleConfig] Skip ${project.name}")
            return false
        }
        if (!isAutoConfigOn(project)) {
            project.logger.info("[GradleConfig] ${AUTO_CONFIG_SWITCH} is off, skipping ${project.name}")
            return false

        }
        //Single project
        if (project.rootProject.allprojects.size() == 1) {
            return true
        }
        //The root project in a multi-project is not configurable
        if (project.depth == 0 && project.path == ":") {
            project.logger.info("[GradleConfig] skipping root project ${project.name}")
            return false
        }
        return true
    }

    static boolean isAutoConfigurable(Settings settings) {
        if (BUILD_SRC == settings.rootProject.name && ROOT_PATH == settings.rootProject.path) {
            logger.info("[GradleConfig] skip BuildSrc")
            return false
        }
        //gradle.properties is not loaded yet, have to load it manually
        DefaultGradlePropertiesLoader propertiesLoader = new DefaultGradlePropertiesLoader(settings.gradle.startParameter)
        logger.info("[GradleConfig] Loading gradle.properties from ${settings.rootDir}")
        propertiesLoader.loadProperties(settings.rootDir)
        Map<String, String> props = propertiesLoader.mergeProperties(new HashMap<String, String>())
        logger.info("[GradleConfig] GradleProperties ${props}")
        if ("true" != props.get(AUTO_CONFIG_SWITCH)) {
            return false
        }
        return true;
    }

    static boolean isGloballyOff(Gradle g) {
        StartParameter parameter = g.getStartParameter()
        return "false" == parameter.getProjectProperties().get(AUTO_CONFIG_SWITCH)
    }

    static boolean isBuildSrc(Project project) {
        BUILD_SRC == project.name && ROOT_PATH == project.path
    }
}

class DiscoverProjectsAction implements Action<Settings> {
    private static final Logger logger = Logging.getLogger(DiscoverProjectsAction.class)

    @Override
    void execute(Settings settings) {
        if (!ConfigUtils.isAutoConfigurable(settings)) {
            logger.info("[GradleConfig][${settings.rootProject.name}] Discoverying projects is skipped...)}")
            return
        }
        def skipDirs = ~/^(build|\..*|src|out)/
        def preDir = {
            if (skipDirs.matcher(it.name).matches())
                return SKIP_SUBTREE
        }

        def getProjectName = { String dir ->
            (dir - (settings.rootDir.toString() + "/")).replaceAll("/", ":")
        }

        settings.rootDir.traverse(type: DIRECTORIES, preDir: preDir) { dir ->
            if (skipDirs.matcher(dir.name)) {
                return
            }
            def buildFileExist = false
            dir.eachFileMatch FILES, ~/build.gradle(?:.kts)?/, { buildFileExist = true }
            logger.info("[GradleConfig][${settings.rootProject.name}] Including project ${dir.name}")
            settings.include getProjectName(dir.toString())
        }
    }
}

class ConfigBuildScriptRepositories implements Action<Gradle> {

    @Override
    void execute(Gradle gradle) {
        config(gradle.rootProject)
    }

    private static void config(Project project) {
        def repositories = project.buildscript.repositories
        def mirrorUrl = project.getProperties().get('nexus.mirror.url')
        def setMavenRepo = { MavenArtifactRepository repo ->
            repo.name("Nexus Mirror")
            repo.url(mirrorUrl)
        }
        if (mirrorUrl) {
            repositories.maven(setMavenRepo)
        }
        def dependencies = project.buildscript.dependencies
        dependencies.add("classpath", "com.palantir.gradle.gitversion:gradle-git-version:0.12.3")
        project.logger.info("[GradleConfig][${project.name}] BuildScript initialization is done.")
    }

}

class ConfigProject implements Action<Project> {

    @Override
    void execute(Project project) {
        if (!ConfigUtils.isAutoConfigurable(project)) {
            return
        }
        project.logger.info("[GradleConfig][${project.name}] Configuring Project ...")
        ["versioning", "project", "repositories", "testing"].each { scriptName ->
            def scriptFile = project.rootProject.file("gradle/autoconfig/before.d/${scriptName}.gradle")
            project.logger.info("[GradleConfig][${project.name}] Applying script file: ${scriptFile.name} ")
            project.apply from: scriptFile
        }
    }
}


class GradleAutoConfigurationPlugin implements Plugin<Gradle> {
    private static final Logger logger = Logging.getLogger(GradleAutoConfigurationPlugin.class)

    @Override
    void apply(Gradle target) {
        if (ConfigUtils.isGloballyOff(target)) {
            logger.info("[GradleConfig] Gradle AutoConfiguration is globally disabled.")
            return
        }
        logger.info("[GradleConfig] Init Script Executing....")
        target.settingsEvaluated(new DiscoverProjectsAction())
        target.projectsLoaded(new ConfigBuildScriptRepositories())
        target.beforeProject(new ConfigProject())
    }

}

apply plugin: GradleAutoConfigurationPlugin
